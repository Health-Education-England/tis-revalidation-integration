plugins {
  id "java"
  id "org.springframework.boot" version "2.5.1"
  id "io.spring.dependency-management" version "1.0.11.RELEASE"

  // Code quality plugins
  id "checkstyle"
  id "jacoco"
  id "org.sonarqube" version "3.2.0"
}

group = "uk.nhs.hee.tis.revalidation"
version = "0.4.0"
sourceCompatibility = "11"

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}


repositories {
  mavenCentral()

  maven {
    name 'hee-ssh-repository'
    url 'https://build.tis.nhs.uk/maven/repository'
    authentication {
      basic(BasicAuthentication)
    }
    credentials {
      username = "gradle"
      password = "jWRg9EfqNMswy3A9vMDwkRQR2RMzgMc94T6sPCVL"
    }
  }
}

dependencies {
  // Spring Boot starters
  implementation "org.springframework.boot:spring-boot-starter-actuator"
  implementation "org.springframework.boot:spring-boot-starter-web"
  testImplementation("org.springframework.boot:spring-boot-starter-test") {
    exclude group: "org.junit.vintage", module: "junit-vintage-engine"
  }

  // Apache Camel starters
  ext.camelVersion = "3.6.0"
  implementation "org.apache.camel.springboot:camel-spring-boot-starter:$camelVersion"
  implementation "org.apache.camel.springboot:camel-http-starter:$camelVersion"
  implementation "org.apache.camel.springboot:camel-servlet-starter:$camelVersion"
  implementation "org.apache.camel.springboot:camel-jackson-starter:$camelVersion"

  // Lombok
  compileOnly "org.projectlombok:lombok"
  annotationProcessor "org.projectlombok:lombok"

  // MapStruct
  implementation "org.mapstruct:mapstruct:1.4.2.Final"
  annotationProcessor "org.mapstruct:mapstruct-processor:1.4.2.Final"
  testAnnotationProcessor "org.mapstruct:mapstruct-processor:1.4.2.Final"

  // Sentry reporting
  ext.sentryVersion = "4.3.0"
  implementation "io.sentry:sentry-spring-boot-starter:$sentryVersion"
  implementation "io.sentry:sentry-logback:$sentryVersion"

  // Keycloak
  implementation "com.transformuk.hee:keycloak-client:3.0.0"
  implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.12.2"

  //RabbitMQ
  implementation "org.springframework.boot:spring-boot-starter-amqp"

  // AWS cloud
  ext.cloudVersion = "2.3.0"
  implementation "io.awspring.cloud:spring-cloud-starter-aws:$cloudVersion"
  implementation "io.awspring.cloud:spring-cloud-starter-aws-messaging:$cloudVersion"

  // Elastic Search
  implementation "org.springframework.boot:spring-boot-starter-data-elasticsearch:2.4.4"

  // Java Persistence
  implementation "javax.persistence:javax.persistence-api:2.2"

  // Jackson Json
  implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.12.3"
}

checkstyle {
  toolVersion = "8.29"
  config = resources.text.fromArchiveEntry(configurations.checkstyle[0], "google_checks.xml")
}

jacocoTestReport {
  reports {
    html.enabled true
    xml.enabled true
  }
}

sonarqube {
  properties {
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.login", System.getenv("SONAR_TOKEN")
    property "sonar.organization", "health-education-england"
    property "sonar.projectKey", "Health-Education-England_tis-revalidation-integration"

    property "sonar.java.checkstyle.reportPaths",
      "build/reports/checkstyle/main.xml,build/reports/checkstyle/test.xml"
  }
}

test {
  finalizedBy jacocoTestReport
  useJUnitPlatform()
}
