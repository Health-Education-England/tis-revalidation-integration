plugins {
  id "java"
  id "org.springframework.boot" version "3.5.10"
  id "io.spring.dependency-management" version "1.1.7"

  // Code quality plugins
  id "checkstyle"
  id "jacoco"
  id "org.sonarqube" version "4.0.0.2929"
}

group = "uk.nhs.hee.tis.revalidation"
version = "0.10.0"

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()

  maven {
    url "https://hee-430723991443.d.codeartifact.eu-west-1.amazonaws.com/maven/Health-Education-England/"
    credentials {
      username "aws"
      password System.env.CODEARTIFACT_AUTH_TOKEN
    }
    content {
      includeGroup "com.transformuk.hee"
      includeGroupByRegex "uk\\.nhs\\.hee(\\..*)?"
    }
  }
}

ext {
  camelVersion = "4.14.4"
  cloudAwsVersion = "3.4.1"
  sentryVersion = "8.31.0"
  mapstructVersion = "1.6.3"
}

dependencies {
  // Spring Boot starters
  implementation "org.springframework.boot:spring-boot-starter-actuator"
  implementation "org.springframework.boot:spring-boot-starter-web"
  implementation "org.springframework.boot:spring-boot-starter-amqp"
  implementation "org.springframework.boot:spring-boot-starter-data-elasticsearch"

  testImplementation("org.springframework.boot:spring-boot-starter-test") {
    exclude group: "org.junit.vintage", module: "junit-vintage-engine"
  }

  // Apache Camel
  implementation platform("org.apache.camel.springboot:camel-spring-boot-bom:${camelVersion}")
  implementation "org.apache.camel.springboot:camel-spring-boot-starter"
  implementation "org.apache.camel.springboot:camel-http-starter"
  implementation "org.apache.camel.springboot:camel-servlet-starter"
  implementation "org.apache.camel.springboot:camel-jackson-starter"
  implementation "org.apache.camel.springboot:camel-aws-xray-starter"

  // Lombok
  compileOnly "org.projectlombok:lombok"
  annotationProcessor "org.projectlombok:lombok"

  // MapStruct
  implementation "org.mapstruct:mapstruct:${mapstructVersion}"
  annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
  testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

  // Sentry
  implementation "io.sentry:sentry-spring-boot-starter:${sentryVersion}"
  implementation "io.sentry:sentry-logback:${sentryVersion}"

  // Keycloak
  implementation "com.transformuk.hee:keycloak-client:3.0.0"

  implementation "io.awspring.cloud:spring-cloud-aws-starter-sqs:${cloudAwsVersion}"

  // Jakarta Persistence API replaces javax.persistence
  implementation "jakarta.persistence:jakarta.persistence-api:3.1.0"

  // Faker
  implementation ("com.github.javafaker:javafaker:1.0.2")
  {
    exclude group: "org.yaml", module: "snakeyaml"
  }
  implementation("org.yaml:snakeyaml:2.4")

  // MongoDB
  ext.mongoDriverVersion = "4.5.1"
  implementation "org.mongodb:mongodb-driver-core:$mongoDriverVersion"
  implementation "org.mongodb:bson:$mongoDriverVersion"
}

checkstyle {
  toolVersion = "10.12.1"
  config = resources.text.fromArchiveEntry(configurations.checkstyle[0], "google_checks.xml")
}

jacocoTestReport {
  reports {
    html.required = true
    xml.required = true
  }
}

sonarqube {
  properties {
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.login", System.getenv("SONAR_TOKEN")
    property "sonar.organization", "health-education-england"
    property "sonar.projectKey", "Health-Education-England_tis-revalidation-integration"
    property "sonar.java.checkstyle.reportPaths",
      "build/reports/checkstyle/main.xml,build/reports/checkstyle/test.xml"
  }
}

test {
  finalizedBy jacocoTestReport
  useJUnitPlatform()
}
